\ Some examples of recursive RPN words

\ rfib is purely stack-based
: rfib
  doc:"rfib   ( n -- fib )
Return Nth Fibonacci number.

DEFINITION:
fib(n) = fib(n-1) + fib(n-2)"
  dup 1 > if
      dup  1 -  recurse
      swap 2 -  recurse
      +
  then
;


\ Interesting to compare to this approximation:
: fib_approx
  doc:"fix_approx   ( n -- ~fib )
Approximate the n'th Fibonacci number."

  | in:n
    sqrt5 sq5plus sq5minus |
  5 sqrt !sqrt5
  1 @sqrt5 + 2 / !sq5plus
  1 @sqrt5 - 2 / !sq5minus
  @sq5plus @n ^  @sq5minus @n ^  -
  @sqrt5 /
;


\ rfact uses an inout: local variable
: rfact
  doc:"rfact   ( x -- x! )
Factorial, recursive.

DEFINITION:
x! = x * (x-1) * (x-2) ... * 2 * 1"
  | inout:x |
  @x 0 > if
      @x dup 1 -  recurse  *  !x
  else
      1 !x
  then
;


\ Don't call this with arguments greater than 2 (maybe 3)
: ack
  doc:"ack   ( y x -- ack )
Ackermann's function.  Neither argument may be negative.

DEFINITION:
            { y+1                       if x==0
ack(x, y) = { ack(x-1, 1)               if y==0
            { ack(x-1, ack(x, y-1))     otherwise

WARNING:
ack is implemented using a recursive definition which will consume
inordinate amounts of space and time even on modest arguments."

  | in:x in:y |
  \ ."ack( " @x . .", " @y . .")" cr    \ If you want to monitor progress

  @x 0= if
      @y 1 +                    \ y+1
  else
      @y 0= if
          1 @x 1 - recurse      \ ack(x-1, 1)
      else
          @y 1 - @x recurse     \ ack(x-1, ack(x, y-1))
          @x 1 -
          recurse
      then
  then
;
