: craps
  doc:"Play a game of craps"
  | initial_stake cash |

  ."Welcome to the craps table!" cr
  20 randint 5 * 100 +
  dup !initial_stake   !cash
  ."I'll stake you " @.initial_stake ."zorkmids to get started." cr

  : place_bet
    | valid bet |   FALSE !valid
    begin
      "How much do you wager (0 to quit)? " prompt int !bet
      @bet 0 < if
        ."You can't bet a negative amount!" cr
      else
        @bet @cash > if
          ."You can't bet more than you have!" cr
        else
          TRUE !valid
        then
      then
    @valid until
    @bet
  ;

  : play
    | in:bet win |
    : toss_dice  6 randint 6 randint + ;

    : play_point
      | in:point |
      begin
        ."You roll a " toss_dice dup . ."... "
        dup 7 = if
          ."You lose on a seven, sorry" cr
          FALSE !win  leave
        else
          dup @point = if
            ."You make your point and win double!" cr
            2 !*bet  \ Pays 2:1
            TRUE !win  leave
          else
            ."Roll again" cr  drop
          then
        then
      again
      drop
    ;

    ."You roll a " toss_dice dup . ."... "
    case
      \ Win
       7 of ."Seven, winner!"        cr TRUE  !win endof
      11 of ."Eleven, lucky winner!" cr TRUE  !win endof
      \ Lose
       2 of ."Snake eyes, you lose!" cr FALSE !win endof
       3 of ."Craps, you lose!"      cr FALSE !win endof
      12 of ."Boxcars, you lose!"    cr FALSE !win endof
      \ Point
      otherwise
        ."Your point is " @.caseval cr
        @caseval play_point
    endcase

    @win not if -1 !*bet then
    @bet !+cash
    ."You now have " @.cash ."zorkmids." cr
  ;

  \ craps main code
  begin
    place_bet
    ?dup 0= if
      | diff |
      @cash @initial_stake - !diff
      @diff 0 > if
        ."After my initial " @.initial_stake ."zorkmid loan, "
        ."you leave with " @.diff ."zorkmids." cr
      else
        -1 !*diff
        ."You still owe me " @.diff ."zorkmids!" cr
      then
      ."Thanks for playing!" cr exit
    then

    play

    @cash 0 <= if
      ."Looks like you're broke - better luck next time!" cr exit
    then
  again
;
