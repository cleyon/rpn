: hanoi         doc:"hanoi   ( n -- )
Solve Towers of Hanoi puzzle with recursion.
Move disks 1..n from tower 1 to tower 2, using 3 as spare."

  | in:n |

  : move_one_disk
    | in:disk in:source in:dest |
    ."Move disk " @.disk ."from " @.source ."to " @.dest cr
  ;

  : move_group_of_disks
    | in:disk in:source in:dest in:spare |

    \ If this is the smallest disk, just move it directly
    @disk 1 = if
      @disk @source @dest move_one_disk
    else
      \ Move N-1 disks from source to spare
      @disk 1 - @source @spare @dest    recurse

      \ Move last disk onto dest
      @disk     @source @dest           move_one_disk

      \ Move previous N-1 disks from spare onto dest
      @disk 1 - @spare  @dest  @source  recurse
    then
  ;

  \ Move N disks from tower 1 to tower 2 using tower 3 as a spare
  @n 0 > if
    @n 1 2 3  move_group_of_disks
  else
    ."Number of disks must be positive" cr
  then
;
